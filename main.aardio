import fonts
import fsys.lnk
import win.ui
import win.ui.tooltip
import win.util.tray
import process.admin
import process.mutex
/*DSG{{*/
mainForm = win.form(cls="FEXC_FORM";text="FilenameExchanger";right=362;bottom=242;acceptfiles=1;border="thin";exmode="toolwindow";max=false;min=false;mode="popup";sysmenu=false;title=false;topmost=1)
mainForm.add(
button_start={cls="button";text="启动 | 啟動";left=118;top=183;right=245;bottom=227;default=1;font=LOGFONT(h=-19;name='微软雅黑');z=3};
edit_path1={cls="edit";left=11;top=59;right=351;bottom=99;edge=1;z=2};
edit_path2={cls="edit";left=11;top=126;right=351;bottom=166;edge=1;z=6};
static_admin={cls="static";text="?";left=222;top=4;right=249;bottom=31;align="center";bgcolor=0x000000;center=1;color=0xFFFFFF;font=LOGFONT(h=-21;name='Microsoft YaHei UI');notify=1;transparent=1;z=12};
static_bg={cls="static";left=0;top=0;right=364;bottom=33;bgcolor=0xD77800;z=1};
static_close={cls="static";text="H";left=328;top=4;right=356;bottom=31;align="center";center=1;color=0xFFFFFF;font=LOGFONT(h=-21;name='Microsoft YaHei UI';weight=700);notify=1;transparent=1;z=8};
static_menu={cls="static";text="F";left=258;top=4;right=285;bottom=31;align="center";bgcolor=0x000000;center=1;color=0xFFFFFF;font=LOGFONT(h=-21;name='Microsoft YaHei UI');notify=1;transparent=1;z=10};
static_min={cls="static";text="G";left=292;top=4;right=319;bottom=31;align="center";center=1;color=0xFFFFFF;font=LOGFONT(h=-21;name='Microsoft YaHei UI');notify=1;transparent=1;z=7};
static_path1={cls="static";text="文件一";left=11;top=39;right=57;bottom=57;font=LOGFONT(name='微软雅黑');transparent=1;z=4};
static_path2={cls="static";text="文件二";left=11;top=106;right=57;bottom=124;font=LOGFONT(name='微软雅黑');transparent=1;z=5};
static_tip={cls="static";text="C";left=184;top=4;right=212;bottom=31;align="center";center=1;color=0x0000FF;font=LOGFONT(h=-21;name='Microsoft YaHei UI';weight=700);notify=1;transparent=1;z=11};
static_top={cls="static";text="B";left=4;top=4;right=31;bottom=31;align="center";center=1;color=0x00FFFF;font=LOGFONT(h=-21;name='Microsoft YaHei UI');notify=1;transparent=1;z=9}
)
/*}}*/

exe_ext = io.splitpath(io._exefile).ext
is_admin = false

if(exe_ext == ".EXE" and (not process.admin.isRunAs())){
    process.admin.runas(,_ARGV)
    return 
}elseif(process.admin.isRunAs()){
	is_admin = true
    import win.messageFilter
    win.messageFilter.change(mainForm.hwnd,1/*_MSGFLT_ADD*/,0x4A/*_WM_COPYDATA*/,0x233/*_WM_DROPFILES*/,0x0049/*_WM_COPYGLOBALDATA*/ )
}else{
	is_admin = false
}



rename_dll := raw.loadDll($"name_exchanger_rs.dll",,"cdecl")



get_output_info = function(id){
	if(id == 0){
        return true, "Success"
    }elseif(id == 1){
        return false, "No Exist"
    }elseif(id == 2){
        return false, "Permission Denied"
    }elseif(id == 3){
        return false, "New File Already Exists"
    }else {
        return false, "UNKNOWN ERROR"
    }
}


argv_len_judge = table.len(_ARGV)
if(argv_len_judge == 0){
    if(io.exist(io.joinpath(io.getSpecial(0x1a/*_CSIDL_APPDATA*/),"Microsoft\Windows\SendTo\name_exchanger.lnk"))){
        lnk_exist = true
    }else{
        lnk_exist = false
    }

    if(exe_ext != ".exe" and exe_ext != ".EXE"){
        io.rename(io._exepath,io._exedir + io.splitpath(io._exepath).name + ".exe")
        mainForm.static_tip.oncommand()
    }
    
    if(not is_admin){
    	mainForm.static_admin.text = "D"
    }else{
    	mainForm.static_admin.text = "E"
    }
	
	custom_font = fonts.addFamily($"/res/NameExchangeCustom.ttf","NameExchangeCustom")
	mainForm.static_top.setFont(custom_font)
	mainForm.static_tip.setFont(custom_font)
	mainForm.static_admin.setFont(custom_font)
	mainForm.static_menu.setFont(custom_font)
	mainForm.static_min.setFont(custom_font)
	mainForm.static_close.setFont(custom_font)

    mainForm.show()
}elseif(argv_len_judge == 2){
	::Kernel32.AttachConsole(-1);//附加到父进程的console
	..io.stdout.reopen("CONOUT$","w+t");
	..io.stderr.reopen("CONOUT$","w+t");

    rename_dll.exchange(_ARGV[1],_ARGV[2])

    return
}else{
    win.msgbox("Error for file error. Please Retry.","Error",,,1000)
    return
}



var mutex = process.mutex("D79BA905DBC642759A74162A2FAABC28")
if(mutex.conflict){
	import win
	mainForm.value = "DESTORY_WINDOW_NAME_EXCHANGER"
	last_hwnd = process.findWindow(,"FEXC_FORM","FilenameExchanger")
	win.show(last_hwnd)
	win.setForeground(last_hwnd)
    return
}



mainForm.static_tip.oncommand = function(id,event){
    win.msgbox("拖入文件即可使用，软件将常驻任务栏，悬停鼠标于
按钮上可获得提示。

软件包含以下功能
〇管理员身份启动
〇创建/删除「发送到」菜单快捷方式
〇置顶

拖入檔案即可使用，軟體將常駐系統匣，懸停鼠標於
按鈕上可獲得提示。

軟體包含以下功能
〇總是以系統管理員執行
〇建立/刪除「傳送到」選單捷徑方式
〇置頂","Warning",,mainForm.hwnd)
}


var tooltipCtrl = win.ui.tooltip(mainForm)
var tip_table = {
    "static_tip" = '关于\n\n關於';
    "static_menu" = '单击可新增右键菜单-“发送到”选项\n右键点击以删除\n\n點擊新增至右鍵選單-“傳送到”選項\n點擊右鍵取消';
    "static_top" = '置顶开关\n\n置頂開關';
    "static_admin" = '自动以管理员身份启动\n\n總是以系統管理員執行';
    "static_min" = '最小化';
    "static_close" = '关闭\n\n關閉';
}
tooltipCtrl.add(tip_table)
mainForm.tray = win.util.tray(mainForm)
mainForm.tray.tip = '左键显示/隐藏|||右键退出\n左鍵顯示/隱藏|||右鍵退出'


mainForm.onDropFiles = function(files){
    select(tonumber(table.len(files))) {
        case 1{
            if(mainForm.edit_path1.text == ""){
                mainForm.edit_path1.text = files[1]
            }elseif(mainForm.edit_path2.text == ""){
                mainForm.edit_path2.text = files[1]
            }else{
                mainForm.edit_path1.text = files[1]
                mainForm.edit_path2.text = ""
            }
        }
        case 2{
            mainForm.edit_path1.text = files[1]
            mainForm.edit_path2.text = files[2]
        }
        else{
            win.msgbox("Error for file error. Please Retry.","Error",,mainForm.hwnd,500)
        }
    }
}

mainForm.button_start.oncommand = function(id,event){
    return_id = rename_dll.exchange(mainForm.edit_path1.text,mainForm.edit_path2.text)
    ok, info = get_output_info(return_id)
    if(ok){
        mainForm.edit_path1.text = ""
        mainForm.edit_path2.text = ""
    }else{
        win.msgbox(info,"Error",,mainForm.hwnd,2000)
    }
}

mainForm.onMouseDown  = function(wParam,lParam){
    mainForm.hitCaption()
}

mainForm.static_close.oncommand = function(id,event){
    mainForm.close()
}

mainForm.static_top.oncommand = function(id,event){
    if(mainForm.static_top.text == "A"){
        win.setTopmost(mainForm.hwnd)
        mainForm.static_top.text = "B"
    }else{
        win.setTopmost(mainForm.hwnd,false)
        mainForm.static_top.text = "A"
    }
}

mainForm.static_min.oncommand = function(id,event){
    mainForm.show(false)
}

mainForm.onTrayMessage = {
    [0x202/*_WM_LBUTTONUP*/] = function(wParam){
        if(win.isVisible(mainForm.hwnd)){
            mainForm.show(false)
        }else{
            mainForm.show(true)
        }
    }
    [0x205/*_WM_RBUTTONUP*/] = function(wParam){
        mainForm.close()
    }
}

creat_lnk = function(judge_bool){
    if(judge_bool){
        io.remove(io.joinpath(io.getSpecial(0x1a/*_CSIDL_APPDATA*/),"Microsoft\Windows\SendTo\name_exchanger.lnk"))
        win.msgbox("Removed","Tips",,mainForm.hwnd,1000)
    }else{
        var send_to_lnk = fsys.lnk()
        send_to_lnk.description = "Lnk to FilenameExchanger"
        send_to_lnk.path = io._exepath
        send_to_lnk.setIcon(io._exepath,0)
        var temp_pth = io.joinpath(io.getSpecial(0x1a/*_CSIDL_APPDATA*/),"Microsoft\Windows\SendTo\")
        send_to_lnk.save(temp_pth,"name_exchanger.lnk")
        win.msgbox("Rebuild","Tips",,mainForm.hwnd,1500)
    }
}

mainForm.static_menu.wndproc = function(hwnd,message,wParam,lParam){
    select(message){
        case 0x202/*_WM_LBUTTONUP*/{
            creat_lnk(false)
        }
        case 0x205/*_WM_RBUTTONUP*/{
            creat_lnk(true)
        }
    }
}

mainForm.static_admin.oncommand = function(id,event){
    if(mainForm.static_admin.text == "D"){
        io.rename(io._exepath,io._exedir + io.splitpath(io._exepath).name + ".EXE")
        mainForm.static_admin.text = "E"
    }else{
        io.rename(io._exepath,io._exedir + io.splitpath(io._exepath).name + ".exe")
        mainForm.static_admin.text = "D"
    }
}

return win.loopMessage()
