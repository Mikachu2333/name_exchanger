//RUN\\AS//
import fsys.lnk
//import win.messageFilter
import win.ui
import win.ui.tooltip
import win.util.tray
/*DSG{{*/
mainForm = win.form(cls="FEXC_FORM";text="Filename Exchanger";right=362;bottom=242;acceptfiles=1;border="thin";exmode="toolwindow";max=false;min=false;mode="popup";sysmenu=false;title=false;topmost=1)
mainForm.add(
button_start={cls="button";text="启动 | 啟動";left=118;top=183;right=245;bottom=227;default=1;font=LOGFONT(h=-19;name='微软雅黑');z=3};
edit_path1={cls="edit";left=11;top=59;right=351;bottom=99;edge=1;z=2};
edit_path2={cls="edit";left=11;top=126;right=351;bottom=166;edge=1;z=6};
static_bg={cls="static";left=0;top=0;right=363;bottom=33;bgcolor=0xD77800;z=1};
static_close={cls="static";text='\u26DD';left=329;top=2;right=356;bottom=29;align="center";center=1;color=0xFFFFFF;font=LOGFONT(h=-26;name='微软雅黑');notify=1;transparent=1;z=8};
static_menu={cls="static";text='\uD83D\uDEE0\uFE0F';left=256;top=2;right=283;bottom=29;align="center";bgcolor=0x000000;center=1;color=0xFFFFFF;font=LOGFONT(h=-20;name='微软雅黑');notify=1;transparent=1;z=10};
static_min={cls="static";text="-";left=292;top=2;right=319;bottom=29;align="center";center=1;color=0xFFFFFF;font=LOGFONT(h=-30;name='微软雅黑');notify=1;transparent=1;z=7};
static_path1={cls="static";text="文件一";left=11;top=39;right=57;bottom=57;font=LOGFONT(name='微软雅黑');transparent=1;z=4};
static_path2={cls="static";text="文件二";left=11;top=106;right=57;bottom=124;font=LOGFONT(name='微软雅黑');transparent=1;z=5};
static_tip={cls="static";text='\u2753';left=222;top=2;right=249;bottom=29;align="center";center=1;color=0x0000FF;font=LOGFONT(h=-22;name='微软雅黑');notify=1;transparent=1;z=11};
static_top={cls="static";text='\u2693';left=4;top=2;right=31;bottom=29;align="center";center=1;color=0x00FFFF;font=LOGFONT(h=-24;name='微软雅黑');notify=1;transparent=1;z=9}
)
/*}}*/

//win.messageFilter.change(mainForm.hwnd,1/*_MSGFLT_ADD*/,0x4A/*_WM_COPYDATA*/,0x233/*_WM_DROPFILES*/,0x0049/*_WM_COPYGLOBALDATA*/ )

//io.open()

rename_dll := raw.loadDll($"name_exchanger_rs.dll",,"cdecl")

mainForm.static_tip.oncommand = function(id,event){
	win.msgbox("拖入文件即可使用，软件将常驻任务栏，悬停鼠标于
按钮上可获得提示。
点击🛠️后将增加“右键菜单-发送到”，支持直接右键使用。
如遇右键菜单失效，请右键点击🛠。
左上角⚓/🔱是置顶开关。

拖入檔案即可使用，軟體將常駐系統匣，懸停鼠標於
按鈕上可獲得提示。
點擊🛠️後可新增功能“右鍵選單-傳送到”，支援直接右鍵使用。
如果右鍵選單失效，請右鍵點擊🛠。
左上方⚓/🔱是置頂開關。","Warning",,mainForm.hwnd)
}

argv_len_judge = table.len(_ARGV)
if(argv_len_judge == 0){
	if(io.exist(io.joinpath(io.getSpecial(0x1a/*_CSIDL_APPDATA*/),"Microsoft\Windows\SendTo\name_exchanger.lnk"))){
		lnk_exist = true
	}else{
		lnk_exist = false
	}
	if(io.splitpath(io._exefile).ext != ".exe"){
		io.rename(io._exepath,io._exedir + io.splitpath(io._exepath).name + ".exe")
		mainForm.static_tip.oncommand()
	}
	mainForm.show()
}elseif(argv_len_judge == 2){
	//io.open()
	result = rename_dll.exchange(_ARGV[1],_ARGV[2])
	//io.print(result)
	mainForm.close()
}else{
	win.msgbox("Error for file error. Please Retry.","Error",,,1000)
	mainForm.close()
}

var tooltipCtrl = win.ui.tooltip(mainForm)
var tip_table = {
	"static_tip" = '关于\n\n關於';
	"static_menu" = '单击可新增右键菜单-“发送到”选项\n右键点击以删除\n\n點擊新增至右鍵選單-“傳送到”選項\n點擊右鍵取消';
	"static_top" = '置顶开关\n\n置頂開關';
	"static_min" = '最小化';
	"static_close" = '关闭\n\n關閉';
}
tooltipCtrl.add(tip_table)
mainForm.tray = win.util.tray(mainForm)
mainForm.tray.tip = '左键显示/隐藏|||右键退出\n左鍵顯示/隱藏|||右鍵退出'


mainForm.onDropFiles = function(files){
	select(tonumber(table.len(files))) {
		case 1{
			if(mainForm.edit_path1.text == ""){
				mainForm.edit_path1.text = files[1]
			}elseif(mainForm.edit_path2.text == ""){
				mainForm.edit_path2.text = files[1]
			}else{
				mainForm.edit_path1.text = files[1]
				mainForm.edit_path2.text = ""
			}
		}
		case 2{
			mainForm.edit_path1.text = files[1]
			mainForm.edit_path2.text = files[2]
		}
		else{
			win.msgbox("Error for file error. Please Retry.","Error",,mainForm.hwnd,500)
		}
	}
}

mainForm.button_start.oncommand = function(id,event){
	result = rename_dll.exchange(mainForm.edit_path1.text,mainForm.edit_path2.text)
	//io.print(result)
	if(result == 0){
		mainForm.edit_path1.text = ""
		mainForm.edit_path2.text = ""
	}elseif(result == 1){
		win.msgbox("No Exist","Error",,mainForm.hwnd,2000)
	}elseif(result == 2){
		win.msgbox("Permission Denied","Error",,mainForm.hwnd,2000)
	}elseif(result == 3){
		win.msgbox("New File Already Exists","Error",,mainForm.hwnd,2000)
	}else {
		win.msgbox("UNKNOWN ERROR","Error",,mainForm.hwnd,2000)
	}
}

mainForm.onMouseDown  = function(wParam,lParam){
	mainForm.hitCaption()	
}

mainForm.static_close.oncommand = function(id,event){
	mainForm.close()
}

mainForm.static_top.oncommand = function(id,event){
	if(mainForm.static_top.text == "🔱"){
		win.setTopmost(mainForm.hwnd)
		mainForm.static_top.text = "⚓"
	}else{
		win.setTopmost(mainForm.hwnd,false)
		mainForm.static_top.text = "🔱"
	}
}

mainForm.static_min.oncommand = function(id,event){
	mainForm.show(false)
}

mainForm.onTrayMessage = {
	[0x202/*_WM_LBUTTONUP*/] = function(wParam){
		if(win.isVisible(mainForm.hwnd)){
			mainForm.show(false)
		}else{
			mainForm.show(true)
		}
	}
	[0x205/*_WM_RBUTTONUP*/] = function(wParam){
		mainForm.close()
	}
}

creat_lnk = function(judge_bool){
	if(judge_bool){
		io.remove(io.joinpath(io.getSpecial(0x1a/*_CSIDL_APPDATA*/),"Microsoft\Windows\SendTo\name_exchanger.lnk"))
		win.msgbox("Removed","Tips",,mainForm.hwnd,1000)
	}else{
		var send_to_lnk = fsys.lnk()
		send_to_lnk.description = "An lnk to filename exchange exe"
		send_to_lnk.path = io._exepath
		send_to_lnk.setIcon(io._exepath,0)
		var temp_pth = io.joinpath(io.getSpecial(0x1a/*_CSIDL_APPDATA*/),"Microsoft\Windows\SendTo\")
		send_to_lnk.save(temp_pth,"name_exchanger.lnk")
		win.msgbox("Rebuild","Tips",,mainForm.hwnd,1500)
	}
}

mainForm.static_menu.wndproc = function(hwnd,message,wParam,lParam){
	select(message){
		case 0x202/*_WM_LBUTTONUP*/{
			creat_lnk(false)
		}
		case 0x205/*_WM_RBUTTONUP*/{
			creat_lnk(true)
		}
	}
}


return win.loopMessage()